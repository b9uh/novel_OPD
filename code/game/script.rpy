# Определение персонажей игры.

define helper = Character("HelperX", color="#363dbc")
define friend = Character("Подруга", color="#e499c4")
define brother = Character("Брат", color="#99e4a3")
define coworker = Character("Коллега", color="#6fc7d6")
define player = Character("Алексей", color="#5d62c4")
define player_name = "Алексей"

image bg room = "images/bg/room.jpg"
image bg happy_kid = "images/bg/happy_kid.jpg"
image bg notebook = "images/bg/notebook.jpg"
image bg message = "images/bg/message.jpg"
image bg office = "images/bg/office.jpg"
image bg unity = "images/bg/unity.jpg"
image bg unity_download = "images/bg/unity_download.jpg"
image bg mobile_message = "images/bg/mobile_message.jpg"
image bg night_room = "images/bg/night_room.jpg"
image bg simple_game = "images/bg/simple_game.jpg"
image bg park = "images/bg/park.jpg"
image bg bench = "images/bg/bench.jpg"
image bg cube_1 = "images/bg/cube_1.jpg"
image bg cube_2 = "images/bg/cube_2.jpg"
image bg game_test = "images/bg/game_test.jpg"
image bg notes = "images/bg/notes.jpg"

define specs_counts = {
    "Гейм-дизайнер" : 0,
    "AI программист" : 0,
    "3D-моделлер" : 0,
    "Тестировщик" : 0,
}
define final_spec = "Гейм-дизайнер"
define specs_order = []
define day = 1

init python:
    def jump_spec(spec):
        if spec == "Гейм-дизайнер":
            return "game_designer"
        if spec == "AI программист":
            return "ai_programmer"
        if spec == "3D-моделлер":
            return "artist_3d"
        if spec == "Тестировщик":
            return "tester"

# Игра начинается здесь:
label start:
    $ player_name = renpy.input("Введите ваше имя:", length=20)
    if player_name == "":
        $ player_name = "Алексей"
        
    $ player = Character(player_name, color="#363dbc")

    jump introduction

    return

# Вступление
label introduction:
    # СЦЕНА 1
    # На экране мелькают изображения: детские рисунки, тетрадь с эскизами, тень с счастливым  ребёнком за компьютером и тд.
    scene bg happy_kid with fade
    player "{cps=25}{i}С самого детства я любил игры. Для кого-то это просто развлечение, но для меня — целые миры, в которых можно быть кем угодно.{/i}{/cps}"
    player "{cps=25}{i}В школе я даже пытался придумать свою игру — рисовал картинки в тетрадке, придумывал героев и правила. Тогда мне казалось, что всё возможно.{/i}{/cps}"

    # СЦЕНА 2
    # ДОБАВИТЬ ВИЗУАЛ Комната героя, вечер. На столе учебники и ноутбук. Герой роняет стопку книг, и из коробки выпадает та самая старая тетрадь с рисунками.
    scene bg room with dissolve
    "Комната героя, вечер. На столе учебники и ноутбук. Герой роняет стопку книг, и из коробки выпадает та самая старая тетрадь с рисунками."
    player "{cps=25}{i}Точно… я совсем про неё забыл. Эта тетрадь… когда-то я верил, что сделаю свою игру. А теперь?..{/i}{/cps}"
    menu:
        "Открыть тетрадь":
            jump open_notebook
        "Спрятать обратно":
            jump hide_the_notebook

    return

# Ветка «Открыть тетрадь»
label open_notebook:
    scene bg notebook with dissolve
    "Герой листает страницы."
    player "{cps=25}{i}Глупо… но всё равно что-то внутри оживает, когда я на это смотрю.{/i}{/cps}"
    "В этот момент приходит сообщение от подруги."
    scene bg message with dissolve
    friend "{cps=50}Ты где пропал? Уже неделю не слышно. Сессия тебя одолела?{/cps}"
    player "{cps=50}Почти. Нашёл старую тетрадь с идеями для  игр.{/cps}"
    friend "{cps=50}Игры? Покажи! Мне уже интересно.{/cps}"
    menu:   
        "Отправить фото тетради":
            jump send_photo
        "Не стоит":
            jump dont_send_photo

    return

# Ветка «Спрятать тетрадь» 
label hide_the_notebook:
    player "{cps=25}{i}Глупости. Это всё детство. Сейчас не время для мечтаний.{/i}{/cps}"
    "Он закрывает тетрадь, кладёт её обратно в коробку и выпрямляется."
    player "{cps=25}{i}Пора взрослеть. Игры меня не прокормят.{/i}{/cps}"
    # Камера медленно отъезжает.
    "На фоне — звук закрывающейся крышки коробки."
    # Экран темнеет, появляется надпись:
    scene black with fade
    pause 0.5
    centered "{b}Вы выбрали забыть мечту.{/b}"
    return

# Ветка «Отправить фото тетради» 
label send_photo:
    friend "{cps=50}Потрясающе! Пусть и детские наброски, но в них есть идея. Давай попробуем сделать что-то по этому?{/cps}"
    player "{cps=50}Ты серьёзно?..{/cps}"
    friend "{cps=50}Абсолютно. Я давно хочу попробовать себя в чём-то большом.{/cps}"

    # ДОБАВИТЬ ВИЗУАЛ (В этот момент в комнату заходит Брат.)
    scene bg room with dissolve
    "В этот момент в комнату заходит Брат."
    brother "{cps=50}И снова ты сидишь в игрушках? Экзамен скоро, а ты мечтаешь о глупостях. Ты же понимаешь, что у нормального программиста есть будущее. А игры — это не серьезно.{/cps}"
    player "{cps=25}{i}Что выбрать?..{/i}{/cps}"
    menu:  
        "Путь геймдева (с Подругой)":
            jump gamedev_path
        "Путь обычного программиста (как брат)":
            jump programmer_path

    return

label dont_send_photo:
    player "{cps=25}{i}Да зачем? Всё это детские глупости. Никому нет дела до моих идей.{/i}{/cps}"
    scene bg room with dissolve
    "Он откладывает телефон и снова открывает ноутбук. Несколько секунд тишины. За окном гудит ветер."
    player "{cps=25}{i}Вот и всё. Просто ещё один вечер. Как всегда...{/i}{/cps}"
    scene black with fade
    pause 0.5
    centered "{b}Вы решили не делиться своей мечтой.{/b}"
    return

# Ветка «Путь обычного программиста» 
label programmer_path:
    player "{cps=25}{i}Брат прав. Игры — это детство. А жизнь — это работа, ответственность, стабильность. Если я хочу чего-то добиться, нужно перестать гоняться за мечтами.{/i}{/cps}"
    "Он убирает тетрадь в ящик, открывает ноутбук."
    scene black with fade

    # СЦЕНА 1. Несколько месяцев спустя
    "Несколько месяцев спустя.."
    player "{cps=25}{i}Сдал все экзамены, устроился на стажировку. Backend-разработка, базы данных, немного автоматизации — ничего особенного, но зато всё понятно.{/i}{/cps}"
    "На экране мелькают строки кода."
    player "{cps=25}{i}Я уже уверенно пишу на Python, знаю SQL, разбираюсь в Git. Счастлив только тогда, когда код наконец работает. Просто цифры и логика, ничего особенного.{/i}{/cps}"
    "Он закрывает ноутбук и смотрит в окно."
    # СЦЕНА 2. Первый офис
    scene bg office with fade
    "Белые стены, тихий гул вентиляторов. Люди печатают код."
    coworker "{cps=50}[player_name], можешь глянуть мой скрипт? Он падает при загрузке данных.{/cps}"
    player "{cps=50}Да, дай сюда. Наверное, не обнулил переменную. Вот, смотри — ошибка здесь.{/cps}"
    "Он быстро исправляет, коллега благодарит."
    player "{cps=25}{i}Руки делают всё автоматически. Иногда мне даже кажется, что я сам стал частью программы.{/i}{/cps}"
    menu:   
        "Выпить кофе и продолжить работать":
            pass
        "Сделать короткий перерыв и подышать воздухом":
            pass
    player "{cps=25}{i}Да какая разница… всё равно надо закончить отчёт.{/i}{/cps}"
    # СЦЕНА 3. Поздний вечер
    "Герой закрывает IDE. На рабочем столе — папка “GameIdea.docx”"
    player "{cps=25}{i}Файл всё ещё здесь. Странно, что я его не удалил...{/i}{/cps}"
    "Открывает, видит старые идеи. На лице лёгкая улыбка — и усталость."
    player "{cps=25}{i}Может, я просто не создан для этого. Мечты — для других.{/i}{/cps}"
    # СЦЕНА 4. Несколько лет спустя
    # ДОБАВИТЬ ВИЗУАЛ??? Нарезка кадров: метро, офис, кофе, экран.
    player "{cps=25}{i}Теперь я middle-разработчик. Учу младших, получаю стабильную зарплату. Но каждый раз, когда вижу игру с интересным миром… внутри будто что-то щёлкает...{/i}{/cps}"
    scene black with fade
    pause 0.5
    centered "{b}Вы выбрали стабильность. \nИногда правильный путь не приносит покоя.{/b}"
    return

# Ветка «Путь геймдева» 
label gamedev_path:
    # СЦЕНА 1. Поддержка
    "Вечер. Герой сидит за компьютером, открыта старая тетрадь. На экране появляется уведомление — сообщение от Подруги."
    scene bg message with dissolve
    friend "{cps=50}Я всё думала про ту твою тетрадь. Слушай, может, и правда попробовать? Не обязательно что-то грандиозное. Маленький проект, просто ради интереса.{/cps}"
    player "{cps=50}Ты серьёзно? У меня нет ни команды, ни опыта. Только парочка туториалов по Unity и куча страхов.{/cps}"
    friend "{cps=50}Зато у тебя есть идея. И желание. А это уже половина дела. Хватит бояться — начни с того, что можешь. Я помогу, чем смогу.{/cps}"

    player "{cps=25}{i}Она говорит это так уверенно, как будто всё просто. Но может, ей стоит поверить хотя бы на этот раз?{/i}{/cps}"
    menu:   
        "Попробовать начать":
            jump try_to_start
        "Отложить идею на потом":
            jump set_aside_idea

    return

# Ветка «Попробовать начать» 
label try_to_start:
    player "{cps=50}Хорошо. Только не обещаю, что всё получится идеально.{/cps}"
    friend "{cps=50}А кто сказал, что должно? Главное — чтобы получилось хоть что-то.{/cps}"
    scene bg unity_download with dissolve
    "Герой ищет информацию, скачивает Unity, открывает пустую сцену, рисует первую кнопку."
    player "{cps=25}{i}Странно. Даже простая серенькая фигура на экране вызывает радость. Пока это не игра… но уже начало.{/i}{/cps}"
    # Короткий переход. На экране телефона — уведомление.
    "На экране телефона — уведомление."
    scene bg mobile_message with dissolve
    friend "{cps=50}Эй, покажи, что у тебя вышло!{/cps}"
    player "{cps=50}Пока ничего особенного. Но работает!{/cps}"
    friend "{cps=50}Видишь? А теперь представь, что ты можешь довести это до конца. Ты всегда мечтал создавать миры — так создай хотя бы один.{/cps}"
    scene bg unity_download with dissolve
    player "{cps=25}{i}Её слова застряли где-то глубоко. Создать свой мир… может, я действительно способен?{/i}{/cps}"
    # СЦЕНА 2. Первые шаги
    scene bg unity with dissolve
    "Прошла неделя. Комната героя — на столе кружка с кофе, открытый Unity, куча вкладок с туториалами."
    player "{cps=25}{i}Не думал, что будет так сложно. Каждый урок — как новая загадка. Я чувствую себя первоклассником.{/i}{/cps}"
    scene black with fade
    "В один момент всё зависает. Ошибка. Экран замирает."
    player "{cps=50}Серьёзно?.. Всё рухнуло. День работы — впустую.{/cps}"
    "Он откидывается на спинку стула. Тишина. За окном моросит дождь."
    player "{cps=25}{i}Может, я зря ввязался? Подруга верит, но я… не уверен, что у меня хватит сил.{/i}{/cps}"
    # Сообщение от Подруги.
    "Сообщение от Подруги."
    scene bg mobile_message with dissolve
    friend "{cps=50}Эй, как дела? Не сдавайся. Я видела столько историй, где люди начинали с нуля. Ты не хуже. Главное — не бросай.{/cps}"
    # Гг (улыбается)
    player "{cps=50}Ладно. Попробую ещё раз.{/cps}"
    # СЦЕНА 3. Ночь экспериментов
    scene bg night_room with dissolve
    "Ночь. Комната освещена только экраном. Герой печатает, пробует, тестирует."
    scene bg simple_game with dissolve # переделать фон под ночь
    player "{cps=25}{i}Получилось! Он двигается! Пусть неловко, пусть криво — но двигается!{/i}{/cps}"
    # На экране — простая модель персонажа, прыгающая через препятствия (как в игре динозавр из google).
    player "{cps=50}Я сделал это… хоть и по гайду, но сам!{/cps}"
    # СЦЕНА 4. Знакомство с Помощником
    scene bg room with dissolve
    "Проходит несколько дней. Герой заходит на форум для разработчиков игр, чтобы задать вопрос о баге."
    player "{cps=50}Привет. Я новичок. Делаю простую 2D-игру, но застрял — персонаж не реагирует на управление. Не могу понять, где ошибка.{/cps}"
    "Через пару минут приходит ответ от пользователя с ником “HelperX”."
    helper "{cps=50}Привет! Не переживай, с этим сталкиваются все. Проверь, к объекту ли прикреплён скрипт, и не конфликтует ли с коллайдером. Если хочешь — скинь проект, посмотрю.{/cps}"
    player "{cps=25}{i}Не ожидал, что кто-то ответит так быстро… и так спокойно. Может, он и правда сможет помочь.{/i}{/cps}"
    "Он отправляет проект. Через пару минут получает ответ."
    helper "{cps=50}Нашёл. У тебя неправильное имя компонента в коде. Попробуй исправить — должно заработать.{/cps}"
    "Герой проверяет, исправляет — персонаж оживает."
    player "{cps=25}{i}Работает!{/i}{/cps}"
    "Он быстро возвращается в чат."
    player "{cps=50}Спасибо! Я бы, наверное, ещё день на это потратил.{/cps}"
    helper "{cps=50}Ха, не переживай. Такое у всех бывает. Главное — не бояться спрашивать.{/cps}"
    # Пауза. Герой задумывается.
    "Герой задумывается."
    player "{cps=50}А ты давно этим занимаешься? Просто интересно, как ты понял, что хочешь делать именно игры?{/cps}"
    helper "{cps=50}О, это отдельная история. Сначала думал, что просто люблю программировать. Потом оказалось, что мне ближе придумывать, как всё будет работать.{/cps}"
    helper "{cps=50}А позже понял — не так важно, кем именно ты себя называешь. Главное — куда тебя тянет больше всего.{/cps}"
    player "{cps=25}{i}Куда тянет... я ведь сам до конца не понимаю. Мне нравится кодить, но не только. Иногда я думаю о сюжете, иногда о том, как всё должно выглядеть...{/i}{/cps}"
    helper "{cps=50}Если ты только начинаешь — не спеши с выбором. Попробуй всё по чуть-чуть: посмотри, что тебе даётся легче, а от чего загораешься.{/cps}"
    helper "{cps=50}В геймдеве много ролей — и все нужны. Главное — понять, в какой из них ты растёшь, а не выгораешь.{/cps}"
    player "{cps=50}Звучит просто… но я не знаю, с чего начать искать это “своё”.{/cps}"
    helper "{cps=50}Начни с вопросов. Что тебе интереснее: создавать правила мира, рисовать этот мир, оживлять его кодом или искать ошибки, чтобы сделать лучше?{/cps}"
    helper "{cps=50}Ответ на эти вопросы приведёт тебя туда, где тебе действительно будет по пути.{/cps}"
    "На лице героя появляется лёгкая улыбка. Он задумчиво смотрит на экран, где открыта его первая сцена в игре."
    player "{cps=25}{i}Создавать, рисовать, оживлять, исправлять… Я даже не думал, что это четыре совершенно разных дороги. Похоже, мне стоит разобраться, куда ведёт моя.{/i}{/cps}"
    helper "{cps=50}Если захочешь — я помогу тебе определиться. Не как учитель, а как тот, кто когда-то стоял на твоём месте.{/cps}"
    player "{cps=50}Договорились. Спасибо… правда.{/cps}"
    scene black with fade
    # Он закрывает ноутбук?????. За окном уже рассвет, кадр со спины героя,птички чирикают.
    "Он выключает ПК. За окном уже рассвет, птички чирикают."

    # Глава "Личное знакомство"

    scene bg park with dissolve
    "Парк. Полдень."
    "Главный герой стоит возле небольшого круглого фонтана и нервно смотрит на людей. Рядом дети запускают бумажные кораблики."
    player "{cps=25}{i}Странно. Встретиться вживую с человеком из интернета… ещё неделю назад я думал, что это всё — лишь мечта.{/i}{/cps}"
    "Рядом появляется деловой мужчина в костюме —  планшет в руках, лёгкая усталость на лице."
    helper "{cps=50}[player_name]?{/cps}"
    player "{cps=50}Да… это я. Ты — HelperX?{/cps}"
    helper "{cps=50}Можешь просто — Хелпер. Ты хотел не советов, а точки опоры. Поэтому я здесь.{/cps}"
    "Они садятся на лавку — между ними фонтаны и немного шумных людей" #, контраст с камерностью разговора
    helper "{cps=50}Видишь, в геймдеве есть много направлений. Но ты пока не понимаешь, куда тянет сильнее.{/cps}"
    helper "{cps=50}Поэтому сначала — маленький тест. Ни правил. Ни “правильных” ответов.{/cps}"
    scene black with fade
    "Он открывает на планшете небольшое приложение — пустой белый экран и 5 вопросов"
    # Список 1: [3, 1, 4, 2]
    # Список 2: [2, 4, 1, 3] 
    # Список 3: [1, 3, 2, 4]
    # Список 4: [4, 2, 3, 1]
    # Список 5: [2, 1, 4, 3]
    # Вопрос 1
    "У тебя новый проект. У тебя только 3 дня. С чего начнёшь?"
    menu:
        "Начать с визуального образа": # 3
            $ specs_counts["3D-моделлер"] += 1;
        "Придумать идею / историю": # 1
            $ specs_counts["Гейм-дизайнер"] += 1;
        "Сначала проверю что может быть сломано / обращу внимание на ошибки": # 4
            $ specs_counts["Тестировщик"] += 1;    
        "Сделать рабочую логику / механику": # 2
            $ specs_counts["AI программист"] += 1;
    # Вопрос 2
    "В команде человек сделал не так, как ты ожидал. Что сделаешь?"
    menu:
        "Попробую {b}переделать часть логики{/b}": # 2
            $ specs_counts["AI программист"] += 1;
        "Попробую {b}воссоздать ошибку{/b}": # 4
            $ specs_counts["Тестировщик"] += 1;
        "Объясню сначала, что {b}имелось в виду{/b} изначально": # 1
            $ specs_counts["Гейм-дизайнер"] += 1;
        "{b}Покажу свой вариант{/b} визуально": # 3
            $ specs_counts["3D-моделлер"] += 1;
    # Вопрос 3
    "Дают скучное, но нужное задание. твоя реакция?"
    menu:
        "Превращу во что-то интересное / переосмыслю": # 1
            $ specs_counts["Гейм-дизайнер"] += 1;
        "Выражу это по-своему визуально": # 3
            $ specs_counts["3D-моделлер"] += 1;
        "Сделаю быстро и точно — без лишнего": # 2
            $ specs_counts["AI программист"] += 1;
        "Попробую поискать границы, где есть вероятность чему-то сломаться": # 4
            $ specs_counts["Тестировщик"] += 1;    
    # Вопрос 4
    "Вышел проект, игрок пишет “мне было скучно”. Как ты думаешь, в чём проблема?"
    menu:
        "Значит где-то ошибка, поищу": # 4
            $ specs_counts["Тестировщик"] += 1;  
        "Значит логика не тянет": # 2
            $ specs_counts["AI программист"] += 1;
        "Значит образ был недостаточный": # 3
            $ specs_counts["3D-моделлер"] += 1;
        "Значит я не донёс идею": # 1
            $ specs_counts["Гейм-дизайнер"] += 1;
    # Вопрос 5
    "День без ограничений. что делал бы?"
    menu:
        "Делал бы поведение / реакции персонажей": # 2
            $ specs_counts["AI программист"] += 1;
        "Придумывал бы новый мир / сюжет": # 1
            $ specs_counts["Гейм-дизайнер"] += 1;
        "Искал бы точки поломки / пределы": # 4
            $ specs_counts["Тестировщик"] += 1;  
        "Создавал бы визуальный образ объектов": # 3
            $ specs_counts["3D-моделлер"] += 1;
    helper "{cps=50}Всё. Достаточно.{/cps}"
    player "{cps=50}… и всё? Это тест?{/cps}"
    helper "{cps=50}Ответы, которых оказалось больше всего — покажут, какую область ты попробуешь первой уже завтра.{/cps}"


    # Сортировка по ключам (по убыванию)
    # sorted_dict_desc = {key: my_dict[key] for key in sorted(my_dict, reverse=True)}
    # print(sorted_dict_desc)  # {'d': 4, 'c': 3, 'b': 2, 'a': 1}
    python:
        specs_order = [k for k, v in sorted(specs_counts.items(), key=lambda x: x[1], reverse=True)]
    
    $ jump_label = jump_spec(specs_order[day - 1])
    jump expression jump_label

    return

# Ветка «Отложить идею на потом» 
label set_aside_idea:
    player "{cps=50}Нет, пока не время. Я не готов.{/cps}"
    friend "{cps=50}А если это “потом” никогда не наступит?{/cps}"
    "Молчание. Подруга оффлайн. Герой сидит перед пустым экраном."
    player "{cps=25}{i}Опять я выбрал привычное. Может, когда-нибудь...{/i}{/cps}"

    return

# Ветка «СЦЕНАРИСТ / ГЕЙМДИЗАЙНЕР» 
label game_designer:
    "День [day]"
    scene bg park with dissolve
    "Парк тихий, воздух прохладный."
    "[player_name] садится на лавку."
    "HelperX уже ждёт."
    helper "{cps=50}Сегодня у тебя одно задание.{/cps}"
    helper "{cps=50}Создай мир.{/cps}"
    player "{cps=50}Здесь? Сейчас?..{/cps}"
    helper "{cps=50}Да.{/cps}"
    helper "{cps=50}Не рисуй.{/cps}"
    helper "{cps=50}Не пиши правила.{/cps}"
    helper "{cps=50}Просто… расскажи.{/cps}"
    helper "{cps=50}Пусть мир появится из слов.{/cps}"
    scene bg notebook with dissolve
    "[player_name] открывает старую тетрадь из детства. Смешные, кривые рисунки."
    helper "{cps=50}Не пытайся сделать идеально. Попробуй сделать честно.{/cps}"
    "[player_name] выдыхает. Смотрит на страницу. Колеблется."
    "Как [player_name] начинает создавать свой мир?"
    scene black with fade
    menu:
        "{b}«С героя»{/b} — он рассказывает о персонаже, который должен изменить мир.":
            "[player_name] касается старого рисунка мальчика у моря."
            player "{cps=50}Главный герой — тот, кто слышит туман.{/cps}"
            player "{cps=50}Он не смелый… но он единственный, кто понимает, что что-то идёт не так.{/cps}"
        "{b}«С места»{/b} — он начинает с описания города или локации.":
            "[player_name] смотрит вдаль, на воду фонтана."
            player "{cps=50}Это остров-город. Он окружён туманом, который невозможно пройти. Никто не помнит, что снаружи.{/cps}"
    "И дальше слова текут сами."
    "Он описывает:"
    "— город, где ночь «включается» внезапно,\n— людей, которые слышат эхо своих страхов,\n— героя, что разговаривает с туманом,"
    "— стражницу, охраняющую пустую башню,\n— музыканта, слышащего город лучше зрячих."
    player "{cps=50}Наверное… это странно.{/cps}"
    helper "{cps=50}Это — живо.{/cps}"
    scene bg park with dissolve
    "Он аккуратно закрывает тетрадь и возвращает её герою."
    helper "{cps=50}Историю можно написать в любой момент.{/cps}"
    helper "{cps=50}Но мир…{/cps}"
    helper "{cps=50}Мир нужно почувствовать.{/cps}"
    # helper "{cps=50}Сегодня ты дал ему дыхание. Завтра — дашь реакцию.{/cps}"

    python:
        if day > 3:
            jump_label = "final"
        else:
            day += 1
            jump_label = jump_spec(specs_order[day - 1])
            

    jump expression jump_label

    return

# Ветка «AI-ПРОГРАММИСТ» 
label ai_programmer:
    "День [day]"
    scene bg park with dissolve
    "[player_name] садится на лавку."
    "HelperX уже ждёт."
    "[player_name] приходит и видит HelperX — он стоит, не сидит. Сегодня он выглядит строже, чем вчера."
    "В руках — маленькая бумажная карточка с нарисованным человечком-палочкой."
    helper "{cps=50}Мир у тебя есть. Теперь дай ему поведение.{/cps}"
    player "{cps=50}Я… не умею программировать игры.{/cps}"
    helper "{cps=50}И не нужно.{/cps}"
    helper "{cps=50}Сегодня — не код. Сегодня — логика.{/cps}"
    helper "{cps=50}Что делает персонаж, когда игрок на него смотрит? Когда подходит? Когда угрожает?{/cps}"
    scene bg bench with dissolve
    "Он кладёт перед главным героем ещё несколько карточек — с домиком, деревом и странным чёрным пятном."
    helper "{cps=50}AI — это не формулы.{/cps}"
    helper "{cps=50}Это характер.{/cps}"
    helper "{cps=50}Попробуй придумать, как этот персонаж реагирует на мир.{/cps}"
    "[player_name] берёт первую карточку — человечка-палочку. Он вдруг понимает: это будто оживление бумажного существа."
    "С чего [player_name] начинает оживлять персонажа?"
    menu:
        "{b}«С реакции на игрока»{/b} — как NPC поведёт себя при встрече.":
            player "{cps=50}Если игрок подходит слишком близко… он отступает, но не убегает — просто осторожничает.{/cps}"
            helper "{cps=50}Интересный старт.{/cps}"
        "{b}«С его цели»{/b} — чего хочет NPC, ещё до вмешательства игрока.":
            "[player_name] смотрит на карточку домика."
            player "{cps=50}Его цель — вернуться домой до наступления ночи. Поэтому он заметит игрока… только если тот будет мешать.{/cps}"
    "[player_name] начинает придумывать всё быстрее:"
    "— что NPC делает утром,\n— что — вечером,\n— как он реагирует на звук шагов,"
    "— что вызывает тревогу,\n— когда он начинает доверять игроку."
    "HelperX слушает и одно только время от времени задаёт точные, короткие вопросы:"
    helper "{cps=50}Почему?{/cps}"
    helper "{cps=50}Зачем?{/cps}"
    helper "{cps=50}Что он чувствует?{/cps}"
    "И с каждым таким вопросом поведение NPC становится объёмнее, как будто на карточку наклеивают всё новые слои."
    helper "{cps=50}Ты сделал главное. Ты дал персонажу не интеллект —  а мотив.{/cps}"
    scene black with fade
    # helper "{cps=50}Завтра ты узнаешь, что у мира есть не только действия. Но и форма.{/cps}"

    python:
        if day > 3:
            jump_label = "final"
        else:
            day += 1
            jump_label = jump_spec(specs_order[day - 1])
            

    jump expression jump_label

    return

# Ветка «3D-моделирование» 
label artist_3d:
    scene bg cube_1 with dissolve
    "День [day]"
    "Офис почти пустой. Кто-то работает в наушниках, кто-то тихо обсуждает что-то у доски. В остальном — фоновый шум вентиляторов и редкие щелчки мышей."
    "[player_name] останавливается у одного из столов. Большой монитор, графический планшет, клавиатура, мышь. Всё аккуратно расставлено, как будто здесь привыкли к порядку."
    "На экране — открытая программа для моделирования и пустая сцена с серой сеткой."
    helper "{cps=50}Попробуй сделать любой простой объект. Без деталей. Просто форма. Посмотрим, как ты с ней справишься{/cps}"
    scene bg cube_2 with dissolve
    "[player_name] кладёт руки на мышь и клавиатуру."
    "Несколько секунд он просто смотрит на пустое пространство, как на чистый лист."
    "Потом нажимает кнопку. В центре сцены появляется куб. Он крутит его, приближает, отдаляет, словно привыкая к самому ощущению управления."
    "Начинает медленно менять форму:"
    "— вытягивает одну сторону вперёд"
    "— чуть сплющивает другую"
    "— сглаживает края"
    "— делает сверху небольшой выступ"
    "Где-то рука срывается — форма получается кривой. Он откатывает действие, возвращает всё назад и пробует заново, аккуратнее."
    "Крутит модель с разных ракурсов. Приближает. Отводит камеру. Смотрит, как форма меняется в перспективе."
    "На экране — простой, но аккуратный объект. Похоже на небольшой техничный контейнер или элемент окружения: ящик, модуль, часть стены — что-то универсальное."
    "Он останавливается на секунду, потом открывает настройки света. Меняет его направление, чтобы посмотреть, как падают тени."
    "Замечает, что в одном месте форма выглядит странно, и немного подправляет край."
    "Сохраняет файл."
    "Потом, немного подумав, сохраняет ещё одну копию — с другим названием."
    helper "{cps=50}Нормально получилось. Ты не делал всё наугад — видно, что искал форму и проверял результат{/cps}"
    player "{cps=50}Я просто… смотрел, как это будет выглядеть со всех сторон{/cps}"
    helper "{cps=50}Это и есть важная часть работы{/cps}"
    "Наступает короткая пауза. [player_name] снова поворачивает модель, немного масштабирует её, проверяет, как она смотрится в сцене рядом с тестовой человеческой фигурой."
    "Только после этого он сохраняет финальную версию."
    "Что [player_name] решает сделать с этой моделью?"
    menu:
        "Оставить как есть — сохранить и закрыть":
            pass
        "Сделать вторую версию — немного изменить форму":
            pass
        "Удалить и начать ещё раз — с другой идеей":
            pass
    helper "{cps=50}На сегодня достаточно. Ты понял, как это работает, и уже сам принимаешь решения. Этого хватит{/cps}"
    "[player_name] закрывает программу и встаёт из-за стола."
    scene bg cube_1 with dissolve
    "Он ещё раз смотрит на тёмный экран монитора, где только что была его модель."
    player "{cps=25}{i}Я смог разобраться{/i}{/cps}"
    "Он выходит из офиса за HelperX"
    # Свет медленно гаснет.
    scene black with fade
    python:
        if day > 3:
            jump_label = "final"
        else:
            day += 1
            jump_label = jump_spec(specs_order[day - 1])
            

    jump expression jump_label

    return

# Ветка «ТЕСТИРОВЩИК» 
label tester:
    scene bg game_test with dissolve
    "День [day]"
    "Офис на другом этаже. На нескольких мониторах — запущенные сборки игр, таблицы, окна с логами."
    "[player_name] садится за свободный компьютер. На экране — открыта ранняя версия игры: пустынная локация, простой интерфейс, минимум деталей."
    helper "{cps=50}Тут не играют. Тут ломают. И проверяют, что именно сломалось{/cps}"
    helper "{cps=50}Запусти сборку и попробуй найти хоть одну ошибку. Любую{/cps}"
    "[player_name] надевает наушники и нажимает Start."
    "Игра загружается. Камера появляется посреди локации. [player_name] делает несколько шагов вперёд, поворачивается, смотрит по сторонам. Всё кажется обычным."
    "[player_name] начинает проверять:"
    "— упирается в стены"
    "— прыгает на разные поверхности"
    "— проходит по краю объектов"
    "— заходит в текстуры с неудобных углов"
    "В какой-то момент персонаж застревает между двумя объектами. Камеру начинает слегка трясти."
    scene bg notes with dissolve
    "Он открывает окно записи и делает пометку."
    scene bg game_test with dissolve
    "В другом месте он замечает, что тень от объекта пропадает, если смотреть на него под определённым углом. Он проверяет ещё раз. Потом ещё."
    # Звук клавиатуры и мышки.
    "Дальше — интерфейс. Кнопки срабатывают не с первого раза. Он фиксирует и это."
    "Работает молча, не торопясь, по очереди проверяя разные вещи. Не пытается «поиграть» — именно проверяет."
    "Рядом спокойно наблюдает HelperX."
    "Через некоторое время [player_name] кладёт наушники на стол"
    player "{cps=50}Нашёл несколько странных моментов{/cps}"
    helper "{cps=50}И ты непросто нашёл, ты смог исправить их. Это важно{/cps}" # смог выявить причину ошибок???
    "Что [player_name] хочет проверить ещё?"
    menu:
        "Физику движения — прыжки, столкновения":
            pass
        "Камеру и обзор":
            pass
        "Интерфейс и кнопки":
            pass
    "После выбора он проверяет выбранную часть ещё несколько минут, находит ещё одну небольшую ошибку и фиксирует её."
    helper "{cps=50}У тебя терпение есть. И внимательность. Многие сдаются раньше{/cps}"
    player "{cps=50}Это напоминает пазл. Только без картинки{/cps}"
    helper "{cps=50}Примерно так{/cps}"
    helper "{cps=50}Ты увидел, как техническая сторона разваливает или держит весь игровой мир. Это тоже важная часть разработки{/cps}"
    "[player_name] встаёт из-за компьютера."
    scene bg notes with dissolve
    "На экране остаётся список найденных им багов."
    "На пару секунд он задерживает на нем взгляд."
    scene black with fade
    # Он задерживает на нём взгляд на пару секунд, экран гаснет.
    python:
        if day > 3:
            jump_label = "final"
        else:
            day += 1
            jump_label = jump_spec(specs_order[day - 1])
            

    jump expression jump_label

    return

label final:
    scene black with fade
    "Вечер тёплый, будто город решил немного поддержать главного героя."
    "Он идёт домой по знакомой улице — той самой, по которой ходил сотни раз. Но впервые дорога кажется… другой."
    player "{cps=25}{i}Эти четыре дня были утомительными.{/i}{/cps}"
    "Он подходит к подъезду, поднимается по лестнице, открывает дверь."
    "Квартира встречает привычной тишиной."
    "[player_name] снимает кроссовки, кидает рюкзак, включает настольную лампу. "
    "Свет падает на стол — и там лежит всё, что он приволок за эти дни:"
    "— помятый листок с наброском мира,
    — карточка с NPC,
    — распечатанная 3D-форма куба,
    — маленькая флешка с логами багов."
    # (одна общая картинка)
    "Он смотрит на это хаотичное нагромождение. Оно выглядит так, будто кто-то пытался собрать пазл, в котором кусочки из разных картин."
    "[player_name] садится за стол и просто… молчит."
    "Потом делает глубокий вдох и закрывает глаза." # Звук вдоха
    "Все дни проходят перед ним, как короткие вспышки:" # Звуки щелчков фотоаппарата лампы
    "— мир, который он придумал"
    "— бумажный человечек, которому он дал мотив"
    "— странная форма, которую он вытянул из пустоты"
    "— раздражающе упрямая багованная дверь в тестовой сборке."
    # (отдельные картинки для каждого описания)
    player "{cps=25}{i}Странно… я ведь правда везде нашёл что-то своё.{/i}{/cps}"
    "Он открывает глаза и смотрит на стол."
    "А потом замечает свою тетрадь — обычную. Открывает её случайно, не думая."
    "На одной из страниц он сам когда-то написал наугад, как напоминание: «выбери то, что делает тебя собой»"
    "Он снова смотрит на стол. На этот хаос из работы, проб и ошибок."
    "Каждый предмет — один шаг. Но только один из них вызывает то самое чувство внутри… как будто что-то щёлкает."
    "Словно мир становится чуть яснее."
    "[player_name] медленно протягивает руку."
    "Берёт один предмет."
    "И понимает: он не выбирает профессию. Он выбирает — кто он есть."
    menu:
        "Взять листок с миром":
            $ final_spec = "Гейм-дизайнер"
        "Взять карточку NPC":
            $ final_spec = "AI программист"
        "Взять флешку с логами":
            $ final_spec = "Тестировщик"
        "Взять 3D-деталь ":
            $ final_spec = "3D-моделлер"
    "[player_name] закрывает ладонь на выбранном предмете — и в тот же миг чувствует странное спокойствие."
    "Не эйфорию, не восторг, не страх. Просто… правильность."
    "Как будто весь шум последних дней вдруг улёгся и оставил только одну, очень тихую, но уверенную мысль:"
    player "{cps=25}{i}Да. Это — моё. [final_spec] — это тот, кем я хочу стать.{/i}{/cps}"
    "Он откладывает предмет на стол аккуратно, словно боится нарушить хрупкое начало чего-то большого. И впервые за долгое время улыбается по-настоящему."
    player "{cps=25}{i}Хорошо, что я попробовал. Хорошо, что не испугался.{/i}{/cps}"
    #Звук щелчка-выключения лампы
    "Он выключает лампу, проходит в свою комнату и ложится, зная, что завтра начнётся не что-то идеальное, не что-то простое — но что-то своё." 
    "И эта мысль засыпает вместе с ним, лёгкая и теплая, как начало новой главы…" #(спокойное-умиротворяющее музыкальное сопровождение)

    return